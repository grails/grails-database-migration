buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
}

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin:"org.grails.grails-gsp"

version "3.0.4.BUILD-SNAPSHOT"
group "org.grails.plugins"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7


repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

configurations {
    documentation
}

dependencies {
    compile "org.liquibase:liquibase-core:$liquibaseVersion"

    compile('org.liquibase.ext:liquibase-hibernate5:3.6') {
        exclude group: 'org.hibernate', module: 'hibernate-core'
        exclude group: 'org.hibernate', module: 'hibernate-entitymanager'
        exclude group: 'org.hibernate', module: 'hibernate-envers'
        exclude group: 'org.liquibase', module: 'liquibase-core'
    }
    compileOnly 'org.hibernate:hibernate-core:5.1.1.Final'

    compileOnly 'org.springframework.boot:spring-boot-starter-logging'
    compileOnly "org.springframework.boot:spring-boot-starter-actuator"
    compileOnly "org.springframework.boot:spring-boot-autoconfigure"
    compileOnly "org.springframework.boot:spring-boot-starter-tomcat"

    compileOnly "org.grails:grails-web-boot"
    compileOnly "org.grails:grails-dependencies"
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
    compileOnly "org.grails.plugins:hibernate5"

    testCompile "org.grails:grails-plugin-testing"

    provided("org.grails:grails-shell") {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

    documentation "org.codehaus.groovy:groovy-all:2.4.7"
}

grailsPublish {
    userOrg = "grails"
    githubSlug = 'grails-plugins/grails-database-migration'
    license {
        name = 'Apache-2.0'
    }
    title = "Grails Database Migration Plugin"
    desc = "Grails Database Migration Plugin"
    developers = [kazukiyamamoto: "Kazuki YAMAMOTO"]
}

asciidoctor {
    resources {
        from('src/docs/images')
        into "./images"
    }

    attributes 'experimental'  : 'true',
            'compat-mode'   : 'true',
            'toc'           : 'left',
            'icons'         : 'font',
            'version'       : project.version,
            'groupId'       : project.group,
            'artifactId'    : project.name,
            'sourcedir'     : "${projectDir}/src/main/groovy",
            'liquibaseVersion': liquibaseVersion
}

task apidocs(type: Groovydoc) {
    source project.files('src/main/groovy')

    destinationDir = new File(buildDir, 'docs/api')
    // Might need a classpath
    docTitle = "Grails Database Migration Plugin ${version}"

    classpath = configurations.documentation
    groovyClasspath = configurations.documentation
}

task docs(type:Copy, dependsOn:[apidocs, asciidoctor])  {
    from "$buildDir/asciidoc/html5"
    into "$buildDir/docs"
}

configurations.testCompile.extendsFrom(configurations.compileOnly)

// For publishing snapshots (either to private artifactory or OSS)
publishing {
    repositories {
        maven {
            def publishUrl = project.hasProperty("artifactoryPublishUrl") ? project.artifactoryPublishUrl : ''
            def repoKey = project.version.endsWith("SNAPSHOT") ? 'plugins-snapshot-local' : 'plugins-release-local'
            url = "$publishUrl/$repoKey"

            credentials {
                credentials {
                    def u = project.hasProperty("artifactoryPublishUsername") ? project.artifactoryPublishUsername : ''
                    def p = project.hasProperty("artifactoryPublishPassword") ? project.artifactoryPublishPassword : ''
                    username = u
                    password = p
                }
            }
        }
    }
}
